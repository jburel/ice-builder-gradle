// **********************************************************************
//
// Copyright (c) 2014-2015 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.0'
    }
}

apply plugin: "com.gradle.plugin-publish"

apply plugin: 'groovy'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile gradleApi()
    compile localGroovy()
}

sourceSets {
    main {
        groovy {
            srcDir "src/main/groovy"
        }
        resources {
            srcDir "src/main/resources"
        }
    }
}

version  = "1.0.0"

def pomName = "build/libs/ice-builder-${project.version}.pom"
task writeNewPom {
    outputs.file file(pomName)
} << {
    pom {
        project {
            url 'https://zeroc.com'
            packaging 'jar'
            groupId 'com.zeroc.gradle'
            artifactId 'ice-builder'
            version project.version
            // TODO: Extend with licensing information.
        }
    }.writeTo(pomName)
}
jar.dependsOn(writeNewPom)

jar {
    archiveName = "ice-builder-${project.version}.jar"
}

pluginBundle {
    website = 'https://github.com/zeroc-ice/ice-builder-gradle'
    vcsUrl = 'https://github.com/zeroc-ice/ice-builder-gradle.git'
    description = 'Plugin to automate the compilation of Slice files to Java'
    tags = ['zeroc', 'ice', 'slice']

    plugins {
        slicePlugin {
            id = 'com.zeroc.gradle.ice-builder'
            displayName = 'Ice Builder for Gradle'
        }
    }
}

//
// Used to build the gradle wrapper to automatically download and install
// the version of gradle needed to build plugin.
//
task wrapper(type: Wrapper) {
    gradleVersion = "2.3"
}
